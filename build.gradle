plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "net.minecraftforge.gradle.forge" version "86b2392"
    id "org.jetbrains.kotlin.jvm" version "1.5.30"
}

version = "1.0.1"
group = "net.wyvest"
archivesBaseName = "SimpleRPC"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientJvmArgs += '-Dfml.coreMods.load=net.wyvest.simplerpc.tweaker.SimpleRPCLoadingPlugin'
    clientRunArgs += '--tweakClass net.wyvest.requisiteessentialloader.RequisiteEssentialTweaker'
}

configurations {
    // Creates an extra configuration that implements `implementation` to be used later as the configuration that shades libraries
    include
    implementation.extendsFrom(include)
}

repositories {
    mavenCentral()
    maven { url 'https://repo.sk1er.club/repository/maven-releases/' }
    maven { url 'https://jitpack.io/' }
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
}

dependencies {
    // Libraries
    provided 'xyz.matthewtgm:Requisite:1.1.1'
    include 'com.github.W-OVERFLOW:RequisiteEssentialLoader:7a4eb4eeb7'
    implementation ('xyz.matthewtgm:RequisiteLaunchwrapper:1.1')
    implementation ('gg.essential:loader-launchwrapper:1.1.0')
    provided 'gg.essential:essential-1.8.9-forge:1300'
    include ('com.github.cbyrneee:KDiscordIPC:ca421698f2') {
        transitive = false
    }
    include('com.google.code.gson:gson:2.8.8')
    include('com.kohlschutter.junixsocket:junixsocket-core:2.4.0')
}

jar {
    manifest.attributes(
            'ModSide': 'CLIENT',
            'FMLCorePlugin': 'net.wyvest.simplerpc.tweaker.SimpleRPCLoadingPlugin',
            'FMLCorePluginContainsFMLMod': true,
            'ForceLoadAsMod': true,
            'TweakClass': 'net.wyvest.requisiteessentialloader.RequisiteEssentialTweaker',
            'TweakOrder': '0'
    )

    enabled = false
}

/**
 * This task simply replaces the `${version}` and `${mcversion}` properties in the mcmod.info with the data from Gradle
 */
processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

// This adds support to ("embed", "shade", "include") libraries into our JAR
shadowJar {
    archiveClassifier.set("")
    configurations = [project.configurations.include]
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    relocate("com.google", "kdiscordipc.google")
    exclude 'LICENSE.md'
    exclude 'pack.mcmeta'
    exclude 'dummyThing'
    exclude '**/module-info.class'
    exclude '*.so'
    exclude '*.dylib'
    exclude '*.dll'
    exclude '*.jnilib'
    exclude 'ibxm/**'
    exclude 'com/jcraft/**'
    exclude 'org/lwjgl/**'
    exclude 'net/java/**'

    exclude 'META-INF/proguard/**'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/versions/**'
    exclude 'META-INF/com.android.tools/**'

    exclude 'fabric.mod.json'
}

// This forces the build task to run shadowJar after
tasks.reobfJar.dependsOn(tasks.shadowJar)

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
}

sourceSets {
    dummy
    main {
        compileClasspath += dummy.output
    }
}